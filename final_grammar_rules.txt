/*
Group 10
Akshay Shukla 2022A7PS0087P
Gobind Singh 2022A7PS0083P
Siddhartha Gotur 2022A7PS0070P
Sriram Sudheer Hebbale 2022A7PS0147P
Granth Jain 2022A7PS0172P
*/


<program> ===> <otherFunctions> <mainFunction>
<mainFunction>===> TK_MAIN <stmts> TK_END
<otherFunctions>===> <function><otherFunctions> | eps
<function>===>TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
<input_par>===>TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par>===>TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR | eps
<parameter_list>===><dataType> TK_ID <remaining_list>
<dataType>===> <primitiveDatatype> |<constructedDatatype>
<primitiveDatatype>===> TK_INT | TK_REAL
<constructedDatatype>===> TK_RECORD TK_RUID | TK_UNION TK_RUID | TK_RUID 
<remaining_list>===>TK_COMMA <parameter_list> | eps
<stmts>===><typeDefinitions> <declarations> <otherStmts><returnStmt>
<typeDefinitions>===> <actualOrRedefined> <typeDefinitions> | eps
<actualOrRedefined> ===> <typeDefinition> | <definetypestmt>
<typeDefinition>===>TK_RECORD TK_RUID <fieldDefinitions> TK_ENDRECORD | TK_UNION TK_RUID <fieldDefinitions> TK_ENDUNION
<fieldDefinitions>===> <fieldDefinition><fieldDefinition><moreFields>
<fieldDefinition>===> TK_TYPE <fieldType > TK_COLON TK_FIELDID TK_SEM
<fieldtype>===> <primitiveDatatype> | <constructedDatatype>
<moreFields>===><fieldDefinition><moreFields> | eps
<declarations> ===> <declaration><declarations>| eps
<declaration>===> TK_TYPE <dataType> TK_COLON TK_ID <global_or_not> TK_SEM
<global_or_not>===>TK_COLON TK_GLOBAL| eps
<otherStmts>===> <stmt><otherStmts> | eps
<stmt>===> <assignmentStmt> | <iterativeStmt>|<conditionalStmt>|<ioStmt>| <funCallStmt>
<assignmentStmt>===><SingleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
<singleOrRecId>===>TK_ID <option_single_constructed>
<option_single_constructed>===> <oneExpansion><moreExpansions> | eps
<oneExpansion>===> TK_DOT TK_FIELDID
<moreExpansions>===> <oneExpansion> <moreExpansions> | eps
<funCallStmt>===><outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
<outputParameters> ==> TK_SQL <idList> TK_SQR TK_ASSIGNOP | eps
<inputParameters>===> TK_SQL <idList> TK_SQR
<iterativeStmt>===> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt><otherStmts> TK_ENDWHILE
<conditionalStmt>===> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt><otherStmts> <elsePart>
<elsePart>===>TK_ELSE <stmt><otherStmts> TK_ENDIF | TK_ENDIF
<ioStmt>===>TK_READ TK_OP <var> TK_CL TK_SEM | TK_WRITE TK_OP <var> TK_CL TK_SEM
<arithmeticExpression> ===> <term> <expPrime>
<expPrime> ===> <lowPrecedenceOperators> <term> <expPrime> | eps
<term>===> <factor> <termPrime>
<termPrime> ===> <highPrecedenceOperators><factor> <termPrime> | eps
<factor> ===> TK_OP <arithmeticExpression> TK_CL | <var>
<highPrecedenceOperator>===> TK_MUL | TK_DIV
<lowPrecedenceOperators> ===> TK_PLUS | TK_MINUS
<booleanExpression>===>TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL | <var> <relationalOp> <var> | TK_NOT TK_OP <booleanExpression> TK_CL
<var>===> <singleOrRecId> | TK_NUM | TK_RNUM
<logicalOp>===>TK_AND | TK_OR
<relationalOp>===> TK_LT | TK_LE | TK_EQ |TK_GT | TK_GE | TK_NE
<returnStmt>===>TK_RETURN <optionalReturn> TK_SEM
<optionalReturn>===>TK_SQL <idList> TK_SQR | eps
<idList>===> TK_ID <more_ids>
<more_ids>===> TK_COMMA <idList> | eps
<definetypestmnt> ===> TK_DEFINETYPE <A> TK_RUID TK_AS TK_RUID
<A> ==> TK_RECORD | TK_UNION


Terminals: 
TK_MAIN, TK_END, TK_FUNID, TK_INPUT, TK_PARAMETER, TK_LIST, TK_SQL, TK_SQR, 
TK_OUTPUT, TK_ID, TK_INT, TK_REAL, TK_RECORD, TK_RUID, TK_UNION, TK_COMMA, 
TK_SEM, TK_COLON, TK_GLOBAL, TK_ASSIGNOP, TK_DOT, TK_CALL, TK_WITH, 
TK_PARAMETERS, TK_WHILE, TK_OP, TK_CL, TK_ENDWHILE, TK_IF, TK_THEN, TK_ELSE, 
TK_ENDIF, TK_READ, TK_WRITE, TK_MUL, TK_DIV, TK_PLUS, TK_MINUS, TK_NOT, 
TK_AND, TK_OR, TK_LT, TK_LE, TK_EQ, TK_GT, TK_GE, TK_NE, TK_NUM, TK_RNUM, 
TK_RETURN, TK_DEFINETYPE, TK_AS, TK_ENDRECORD, TK_ENDUNION, TK_FIELDID, 
TK_TYPE


Non Terminals:
<program>, <mainFunction>, <otherFunctions>, <function>, <input_par>, 
<output_par>, <parameter_list>, <dataType>, <primitiveDatatype>, 
<constructedDatatype>, <remaining_list>, <stmts>, <typeDefinitions>, 
<actualOrRedefined>, <typeDefinition>, <fieldDefinitions>, <fieldDefinition>, 
<fieldtype>, <moreFields>, <declarations>, <declaration>, <global_or_not>, 
<otherStmts>, <stmt>, <assignmentStmt>, <singleOrRecId>, 
<option_single_constructed>, <oneExpansion>, <moreExpansions>, <funCallStmt>, 
<outputParameters>, <inputParameters>, <iterativeStmt>, <conditionalStmt>, 
<elsePart>, <ioStmt>, <arithmeticExpression>, <expPrime>, <term>, <termPrime>, 
<factor>, <highPrecedenceOperator>, <lowPrecedenceOperators>, 
<booleanExpression>, <var>, <logicalOp>, <relationalOp>, <returnStmt>, 
<optionalReturn>, <idList>, <more_ids>, <definetypestmnt>, <A>
    